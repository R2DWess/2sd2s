subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'info.solidsoft.pitest'

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(23)
		}
		withSourcesJar()
		withJavadocJar()
	}

	repositories {
		mavenCentral()
		mavenLocal()
	}

	tasks.named('test') {
		useJUnitPlatform()
		maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
		jvmArgs = ['-Xmx2048m']
	}

	tasks.withType(JavaCompile) {
		options.encoding = 'UTF-8'
		options.incremental = true
	}

	group = 'com.corp.connexus'
	version = '0.0.1-SNAPSHOT'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		compileOnly 'org.projectlombok:lombok'
		runtimeOnly 'com.mysql:mysql-connector-j'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
		pitest 'org.pitest:pitest-junit5-plugin:1.0.0'
	}

	pitest {
		targetClasses = ['com.corp.connexus.hr.*']
		targetTests = ['com.corp.connexus.hr.*Test']

		excludedClasses = [
				'com.corp.connexus.hr.model.*',
				'com.corp.connexus.hr.config.*',
				'com.corp.connexus.hr.exception.*',
				'com.corp.connexus.hr.enums.*'
		]

		excludedMethods = ['toString', 'hashCode', 'equals', 'get*', 'set*']

		testSourceSets = [sourceSets.test]
		mainSourceSets = [sourceSets.main]

		junit5PluginVersion = '1.0.0'

		jvmArgs = ['-Xmx1024m']

		outputFormats = ['XML', 'HTML']
		timestampedReports = false

		useClasspathFile = true

		fileExtensionsToFilter.addAll('xml', 'orbit')

		threads = 8

		pitestVersion = '1.15.0'

		withHistory = true
		historyInputLocation = file("$buildDir/pitestHistory")
		historyOutputLocation = file("$buildDir/pitestHistory")
	}
}